#------------------------------------------------------------------------------
# lipophilic_tk.model
#
# A toxicokinetic (TK) model describing the amounts and concentrations of a
# lipophilic substance in a mother before pregnancy, in the mother and her
# fetus(es) during pregnancy, and in the mother and her infant(s) after
# pregnancy. The model allows for: dosing directly to mother and infant(s)
# via dose administration parameters; elimination in all represented organisms
# based on a biological half-life parameter; and transfer from mother to
# infant(s) via breast milk based on a collection of parameters describing
# ingestion rates and concentration in breast milk. The model can also be used
# to describe the amounts and concentrations of a substance in an adult
# organism that is not pregnant or lactating.
#
# Author: Dustin Kapraun, U.S. EPA, December 2018
#
# Notes:
# -- When simulating an exposure scenario that occurs during pregnancy, the
#    parameter describing mass of the infant (M_i_in) should be set to zero
#    and the ratio of concentrations in fetus and mother (r_f_m) will be used
#    to calculate concentration in the fetus based on concentration in the
#    mother. In order to ensure conservation of mass in a simulation that
#    spans pregnancy and the postnatal period, the ratio of concentrations in
#    mother and mother + fetus(es) (r_m_mf_in) should be set to (total mass of
#    mother and infant(s) at birth) divided by (mass of mother at birth plus
#    r_f_m times total mass of infant(s) at birth) at the moment of birth.
#    Similarly, to ensure conservation of mass in a simulation that spans
#    pregnancy and a period before conception, the ratio of concentrations in
#    mother and mother + fetus(es) (r_m_mf_in) should be set to 1.0 at the
#    moment of conception.
# -- When simulating an exposure scenario that occurs during the postnatal
#    period, the parameter describing the mass of the infant (M_i_in) should be
#    positive. The parameters describing the ratio of the concentrations in the
#    mother and mother + fetus(es) (r_m_mf_in) and ratio of the concentrations
#    in fetus(es) and mother (r_f_m) will not be used to calculate values of
#    any state variables or output variables whenever M_i_in is positive.
# -- In order to simulate an exposure scenario in an adult organism that is
#    not pregnant or lactating (and perhaps not female), the parameter
#    describing mass of the infant (M_i_in) should be set to zero and the
#    parameter describing the ratio of concentrations in the mother and mother
#    + fetus(es) (r_m_mf) should be set to 1.0. In such a simulation, the state
#    variable A_mf will represent the total amount of substance in the adult
#    organism, the parameter M_mf_in will represent the mass of the adult
#    organism, and the output variable C_m will represent the concentration in
#    the adult organism.
#
# References:
# -- U.S. EPA (1988). Recommendations for and documentation of biological
#    values for use in risk assessment. U.S. Environmental Protection Agency,
#    Office of Research and Development, Office of Health and Environmental
#    Assessment.
#------------------------------------------------------------------------------


#------------------------------------------------------------------------------
# STATE VARIABLES for the model (for which ODEs are provided).

States = {
    A_mf,       # Amount of substance in the mother + fetus(es) (mg).
    A_i,        # Amount of substance in one infant (mg).
    AUC_m,      # Area under the concentration curve for the mother (mg/kg*d).
    AUC_i,      # Area under the concentration curve for the fetus/infant
                # (mg/kg*d).
    d_m,        # Rate of dose administration to mother (mg/kg/d or mg/kg).
    d_i,        # Rate of dose administration to infant (mg/kg/d or mg/kg).
    T_in,       # Total cumulative input to system (mg).
    T_out,      # Total cumulative output/clearance from system (mg).
};

# Note that rates of dose administration ("d_m" and "d_i") should be given in mg
# per kg of body mass per d if the parameter "food_dose" is FALSE and should be
# given in mg per kg of food if that parameter is TRUE.

# End of STATE VARIABLES.
#------------------------------------------------------------------------------


#------------------------------------------------------------------------------
# OUTPUT VARIABLES for the model (which can be obtained at any point in time
# as analytic functions of state variables, inputs, and parameters).

Outputs = {
    C_m,        # Concentration of substance in the mother (mg/kg).
    C_i,        # Concentration of substance in the fetus/infant (mg/kg).
    M_mf,       # Mass of the mother + fetus(es) (kg).
    M_i,        # Mass of the infant (kg).
    D_m,        # Rate of dose administration to mother (mg/kg/d).
    D_i,        # Rate of dose administration to infant (mg/kg/d).
    R_milk,     # Rate of breast milk ingestion by each infant (kg/d).
    r_m_mf,     # Ratio of concentrations in mother and mother + fetus(es).
    A_bal,      # Mass balance amount (mg). (Should be zero at all times.)
};

# Note that rates of dose administration ("D_m" and "D_i") will *always* be
# given in mg per kg of body mass per d by performing an adjustment to "d_m" and
# "d_i" when the parameter "food_dose" is TRUE.

# End of OUTPUT VARIABLES.
#------------------------------------------------------------------------------


#------------------------------------------------------------------------------
# INPUT VARIABLES for the model (which are independent of other variables, and
# which may vary in time).
Inputs = {
    M_mf_in,    # Mass of the mother + fetus(es) (kg).
    M_i_in,     # Mass of the infant (kg).
    R_milk_in,  # Rate of breast milk ingestion by each infant (kg/d).
    r_m_mf_in,  # Ratio of concentrations in mother and mother + fetus(es).
};

# Note that mass of the infant should be zero for all simulation times
# corresponding to pregnancy and the period before conception and nonzero for
# all simulation times corresponding to the postnatal period. 

# End of INPUT VARIABLES.
#------------------------------------------------------------------------------


#------------------------------------------------------------------------------
# PARAMETERS for the model (which are independent of time).
# Default values are for Aroclor 1254 in rat.

# Substance- and species-specific parameters.
half_life = 70.0;   # Half-life of substance in this species (d).
F_m = 0.094;        # Proportion of the mother's body that is fat.
F_milk = 0.154;     # Proportion of the breast milk that is fat.
r_f_m = 0.35;       # Ratio of concentrations in fetus(es) and mother.

# Experimental parameters.
F_abs = 0.9;        # Fraction of administered dose that is absorbed.
n_i = 10;           # Number of infants.
food_dose = 0;      # A boolean flag. Dose is delivered in food if nonzero.


# Parameters to be computed in MODEL INITIALIZATION. The values of these
# parameters depend on values of the parameters already defined.
k = 0.0;    # First order clearance rate of substance in this species (/d).

# End of PARAMETERS.
#------------------------------------------------------------------------------


#------------------------------------------------------------------------------
# MODEL INITIALIZATION section.
Initialize {
    # ----- Calculated Parameters -----
    # Here we compute values of parameters that depend on constant parameters
    # that have already been defined.

    # First order clearance rate of substance in this species (/d).
    k = log(2) / half_life;

    # ----- State Variables -----

    # Assign an initial value for each state.
    A_mf = 0.0;    # Amount of substance in the mother + fetus(es) (mg).
    A_i = 0.0;     # Amount of substance in the infant (mg).
    AUC_m = 0.0;   # Area under concentration curve for mother (mg/kg*d).
    AUC_i = 0.0;   # Area under concentration curve for fetus/infant (mg/kg*d).
    d_m = 0.0;     # Rate of dose administration to mother (mg/kg/d or mg/kg).
    d_i = 0.0;     # Rate of dose administration to infant (mg/kg/d or mg/kg).
    T_in = 0.0;    # Total cumulative input to system (mg).
    T_out = 0.0;   # Total cumulative output/clearance from system (mg).

}
# End of MODEL INITIALIZATION.
#------------------------------------------------------------------------------


#------------------------------------------------------------------------------
# DYNAMICS section.
Dynamics {
    # Output variables describing input (forcing) functions.
    M_mf = M_mf_in;
    M_i = M_i_in;
    R_milk = R_milk_in;
    r_m_mf = r_m_mf_in;

    # Concentration of substance (mg/kg) in ...
    C_m = A_mf / M_mf * (M_i > 0 ? 1.0 : r_m_mf);   # ... mother.
    C_i = (M_i > 0 ? A_i / M_i : r_f_m * C_m);      # ... fetus/infant.
    C_i = (t < 0 ? 0 : C_i);    # ... zero before conception.

    # Administered doses (mg/kg/d). Convert in-food doses (mg/kg) to effective
    # administered doses if necessary. The conversion formula is based on
    # Equation 6-1 of U.S. EPA (1988). 
    D_m = (food_dose ? 0.065 * pow(M_mf, 0.7919) / M_mf : 1.0) * d_m;
    D_i = (food_dose ? 0.065 * pow(M_i, 0.7919)
        / (M_i > 0 ? M_i : 1.0e-8) : 1.0) * d_i;

    # Mass balance (should be zero at all times).
    A_bal = T_in - A_mf - n_i * A_i - T_out;

    # ----- Time rates of change of state variables (ODEs) -----

    # Rate of change of amount in mother + fetus(es) (mg/d).
    dt(A_mf) = F_abs * D_m * M_mf - k * A_mf - n_i * R_milk * F_milk
        * A_mf / (F_m * M_mf);

    # Rate of change of amount in infant (mg/d).
    dt(A_i) = F_abs * D_i * M_i - k * A_i + R_milk * F_milk
        * A_mf / (F_m * M_mf);

    # Rate of change of area under concentration curve (mg/kg) for ...
    dt(AUC_m) = C_m;    # ... mother.
    dt(AUC_i) = C_i;    # ... fetus/infant.

    # Rate of change of rate of dose administration (mg/kg/d^2) to...
    dt(d_m) = 0.0;      # ... mother.
    dt(d_i) = 0.0;      # ... infant.
    
    # Note that rates of dose administration are set and modified externally
    # (i.e., as "initial conditions" or "events").

    # Rate of change of total cumulative input to system (mg/d).
    dt(T_in) = F_abs * D_m * M_mf + n_i * F_abs * D_i * M_i;

    # Rate of change of total cumulative output/clearance from system (mg/d).
    dt(T_out) = k * (A_mf + n_i * A_i);
}
# End of DYNAMICS.
#------------------------------------------------------------------------------


End.